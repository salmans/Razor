Razor's Input Syntax
====================
theory      ::= [sequent ";"]*
sequent     ::= body "=>" head | head | "~" body
body        ::= conjunctive
head        ::= [existential "|"]* existential
existential ::= ["exists" variable "."]* conjunctive
conjunctive ::= [atom "&"]* atom
atom        ::= identifier terms? | term "=" term | "Truth" | "Falsehood"
terms       ::= "(" [[term ","]* term]? ")"
term        ::= identifier terms | variable | constant
constant    ::= "'" identifier
variable    ::= identifier
identifier  ::= [_a-zA-Z][_a-zA-Z0-9]*


## Operators
Precedence (from high to low):
=
&
|
=>
